plugins {
    id 'io.spring.dependency-management' version '1.1.7' apply false
    id 'org.springframework.boot' version '4.0.3' apply false
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

ext {
    set('springCloudAzureVersion', '7.0.0')
    set('springModulithVersion', '2.0.3')
}

subprojects {
    repositories {
        mavenCentral()
    }

    plugins.withId('java') {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(21)
            }
        }

        dependencies {
            testImplementation 'org.junit.jupiter:junit-jupiter'
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        }

        tasks.withType(Test).configureEach {
            useJUnitPlatform()
        }
    }

    plugins.withId('io.spring.dependency-management') {
        dependencyManagement {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:4.0.3"
                mavenBom "org.junit:junit-bom:5.10.0"
                mavenBom "org.springframework.modulith:spring-modulith-bom:${springModulithVersion}"
                mavenBom "com.azure.spring:spring-cloud-azure-dependencies:${springCloudAzureVersion}"
            }
        }
    }
}